re ::= (literal_star | literal_brace)? ((atom | literal_dollar)* atom)? "$"?
atom ::=
    # Note: "]" is not excluded.
    | [^.\n*$\[\\]
    | "."
    | class
    | "\\<"
    | "\\>"
    | "\\n"
    | "\\" [1-9]
    # Note: "\\}" is not excluded.
    # BUG: "\\\0" is not checked and overruns.
    | "\\" [^()<>{\nn0-9]

    | … "*"+ # Repeated stars are redundant
    | group
    | … range
literal_star ::= "*"
literal_dollar ::= "$"
literal_brace ::= "\\{"
# BUG: Unclosed parens are not checked (not represented here).
group ::= "\\(" literal_star? … "\\)" literal_star?
# Note: Ranges like "[x-y-z]" are allowed, meaning "[x-yy-z]".
# BUG: The range end does not check "\0" and overruns.
class ::= "[" "^"? [^\]\0] ([^\]\0\-] | "-" [^\]])* "]"
# The lower bound must be less than or equal to the upper bound.
range ::= "\\{" bound ("," bound)? "\\}"
bound ::=
    | [0-9]* # Finite (0 if empty; max 255)
    | "\\"   # Infinity (255)
